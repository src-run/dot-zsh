#!/usr/bin/env zsh

#
# This file is part of the `src-run/dot-zsh` project.
#
# (c) Rob Frawley 2nd <rmf@src.run>
#
# For the full copyright and license information, view the LICENSE.md
# file distributed with this source code.
#

#
# A netcat implementation in zsh.
#

zmodload -F zsh/net/tcp +b:ztcp
zmodload -F zsh/zselect +b:zselect
zmodload -F zsh/system +b:syserror +b:sysread

autoload -U sysread

function znc {
    emulate -L zsh
    setopt err_return no_unset warn_create_global

    if [[ $# -ne 2 ]]; then
        print -u 2 "Usage: ${0} host port"
        return 1
    fi

    ztcp -- "${@}"
    integer socket=$REPLY
    trap "ztcp -c ${socket}" EXIT

    while true; do
        typeset -A fds
        zselect -A fds -r 0 -r ${socket}

        integer fd
        for fd in ${(k)fds}; do
            integer ret=0
            case ${fd} in
                0)
                    sysread -p -i 0 -o ${socket} -t 0 || ret=$?
                    ;;
                *)
                    sysread -p -i ${socket} -o 1 -t 0 || ret=$?
                    ;;
            esac
            if [[ ${ret} -ne 0 ]]; then
                [[ ${ret} -eq 5 ]] && ret=0
                return ${ret}
            fi
        done
    done
}

znc "${@}"
